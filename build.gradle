// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }

    ext {
        repoName = 'maven'

        site = 'https://github.com/ximity-oss/dagger-kotlin-mvp'
        gitConnection = 'scm:git:git://github.com/ximity-oss/dagger-kotlin-mvp'
        gitDeveloperConnection = 'scm:git:ssh://git@github.com/ximity-oss/dagger-kotlin-mvp'

        libraryGroup = 'net.ximity.mvp'
        libraryVersion = '0.0.3'
        libraryCode = 1

        developerId = 'ximity-inc'
        developerName = 'Ximity, Inc.'

        licenseName = 'The Apache Software License, Version 2.0'
        licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        allLicenses = ["Apache-2.0"]
    }

    ext.versions = [
            'code'      : ext.libraryCode,
            'name'      : ext.libraryVersion,
            'min'       : 15,

            'compile'   : 27,
            'plugin'    : '3.1.1',
            'build'     : '27.0.3',
            'support'   : '27.1.0',

            'bintray'   : '1.8.0',
            'dexcount'  : '0.8.2',
            'maven'     : '2.1',

            'autocommon': '0.10',
            'dagger'    : '2.15',
            'inject'    : '1',
            'javax'     : '1.3.2',
            'kotlinpoet': '0.7.0',
            'kotlin'    : '1.2.31',
            'javapoet'  : '1.10.0',
    ]

    ext.deps = [
            plugin   : [
                    'android': "com.android.tools.build:gradle:${versions.plugin}",
                    'kotlin' : "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
                    'bintray': "com.jfrog.bintray.gradle:gradle-bintray-plugin:${versions.bintray}",
                    'maven'  : "com.github.dcendents:android-maven-gradle-plugin:${versions.maven}",
            ],

            'auto'   : [
                    'common': "com.google.auto:auto-common:${versions.autocommon}",
            ],
            'dagger' : [
                    'library'    : "com.google.dagger:dagger:${versions.dagger}",
                    'compiler'   : "com.google.dagger:dagger-compiler:${versions.dagger}",
                    'annotations': "javax.annotation:javax.annotation-api:${versions.javax}",
                    'inject'     : "javax.inject:javax.inject:${versions.inject}",
            ],
            'kotlin' : "org.jetbrains.kotlin:kotlin-stdlib-jre7:${versions.kotlin}",
            'poet'   : [
                    'java'  : "com.squareup:javapoet:${versions.javapoet}",
                    'kotlin': "com.squareup:kotlinpoet:${versions.kotlinpoet}",
            ],
            'support': [
                    'appcompatv7': "com.android.support:appcompat-v7:${versions.support}",
                    'annotations': "com.android.support:support-annotations:${versions.support}",
            ],
    ]
}



subprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    buildscript {
        repositories {
            google()
            jcenter()
            mavenCentral()
        }

        dependencies {
            classpath deps.plugin.android
            classpath deps.plugin.kotlin
            classpath deps.plugin.bintray
            classpath deps.plugin.maven
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
