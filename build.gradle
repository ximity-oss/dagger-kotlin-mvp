// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }

    ext {
        repoName = 'maven'

        site = 'https://github.com/ximity-oss/dagger-kotlin-mvp'
        gitConnection = 'scm:git:git://github.com/ximity-oss/dagger-kotlin-mvp'
        gitDeveloperConnection = 'scm:git:ssh://git@github.com/ximity-oss/dagger-kotlin-mvp'

        libraryGroup = 'net.ximity.mvp.kotlin'
        libraryVersion = '0.0.1'
        libraryCode = 1

        developerId = 'ximity-inc'
        developerName = 'Ximity, Inc.'

        licenseName = 'The Apache Software License, Version 2.0'
        licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        allLicenses = ["Apache-2.0"]
    }

    ext.versions = [
            'code'       : ext.libraryCode,
            'name'       : ext.libraryVersion,
            'min'        : 15,

            'compile'    : 27,
            'plugin'     : '3.1.0',
            'build'      : '27.0.3',
            'support'    : '27.1.0',

            'bintray'    : '1.8.0',
            'dexcount'   : '0.8.2',
            'maven'      : '2.1',
            'runtime'    : '4.1.1.4',

            'autocommon' : '0.10',
            'autoservice': '1.0-rc4',
            'dagger'     : '2.15',
            'hamcrest'   : '1.3',
            'inject'     : '1',
            'javax'      : '1.3.2',
            'javapoet'   : '1.10.0',
            'kotlin'     : '1.2.31',
    ]

    ext.deps = [
            plugin   : [
                    'android': "com.android.tools.build:gradle:${versions.plugin}",
                    'kotlin' : "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
            ],

            'auto'   : [
                    'common' : "com.google.auto:auto-common:${versions.autocommon}",
                    'service': "com.google.auto.service:auto-service:${versions.autoservice}",
            ],
            dagger   : [
                    'library'    : "com.google.dagger:dagger:${versions.dagger}",
                    'compiler'   : "com.google.dagger:dagger-compiler:${versions.dagger}",
                    'annotations': "javax.annotation:javax.annotation-api:${versions.javax}",
                    'inject'     : "javax.inject:javax.inject:${versions.inject}",
            ],
            kotlin   : "org.jetbrains.kotlin:kotlin-stdlib-jre7:${versions.kotlin}",
            'support': [
                    'appcompatv7': "com.android.support:appcompat-v7:${versions.support}",
                    'annotations': "com.android.support:support-annotations:${versions.support}",
            ],
    ]
}

subprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    buildscript {
        repositories {
            google()
            jcenter()
            mavenCentral()
        }

        dependencies {
            classpath deps.plugin.android
            classpath deps.plugin.kotlin
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
