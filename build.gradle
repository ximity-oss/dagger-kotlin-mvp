// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.22.0'
    }

    ext {
        repoName = 'maven'

        site = 'https://github.com/ximity-oss/dagger-kotlin-mvp'
        gitConnection = 'scm:git:git://github.com/ximity-oss/dagger-kotlin-mvp'
        gitDeveloperConnection = 'scm:git:ssh://git@github.com/ximity-oss/dagger-kotlin-mvp'

        libraryGroup = 'net.ximity.mvp'
        libraryVersion = '0.0.15'
        libraryCode = 1

        developerId = 'ximity-inc'
        developerName = 'Ximity, Inc.'

        licenseName = 'The Apache Software License, Version 2.0'
        licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        allLicenses = ["Apache-2.0"]
    }

    ext.versions = [
            'code'      : ext.libraryCode,
            'name'      : ext.libraryVersion,
            'min'       : 19,

            'compile'   : 28,
            'plugin'    : '3.5.0',

            'bintray'   : '1.8.4',
            'dexcount'  : '0.8.2',
            'maven'     : '2.1',

            'annotation': '1.1.0',
            'appcompat' : '1.1.0-rc01',
            'autocommon': '0.10',
            'dagger'    : '2.24',
            'inject'    : '1',
            'javax'     : '1.3.2',
            'kotlinpoet': '1.3.0',
            'kotlin'    : '1.3.50',
            'javapoet'  : '1.11.1',
    ]

    ext.deps = [
            plugin   : [
                    'android': "com.android.tools.build:gradle:${versions.plugin}",
                    'kotlin' : "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
                    'bintray': "com.jfrog.bintray.gradle:gradle-bintray-plugin:${versions.bintray}",
                    'maven'  : "com.github.dcendents:android-maven-gradle-plugin:${versions.maven}",
            ],

            'auto'   : [
                    'common': "com.google.auto:auto-common:${versions.autocommon}",
            ],
            'dagger' : [
                    'library'    : "com.google.dagger:dagger:${versions.dagger}",
                    'compiler'   : "com.google.dagger:dagger-compiler:${versions.dagger}",
                    'annotations': "javax.annotation:javax.annotation-api:${versions.javax}",
                    'inject'     : "javax.inject:javax.inject:${versions.inject}",
            ],
            'kotlin' : [
                    'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
                    'reflect': "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
            ],
            'poet'   : [
                    'java'  : "com.squareup:javapoet:${versions.javapoet}",
                    'kotlin': "com.squareup:kotlinpoet:${versions.kotlinpoet}",
            ],
            'jetpack': [
                    'appcompat'  : "androidx.appcompat:appcompat:${versions.appcompat}",
                    'annotations': "androidx.annotation:annotation:${versions.annotation}",
            ],
    ]
}

subprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    buildscript {
        repositories {
            google()
            jcenter()
            mavenCentral()
        }

        dependencies {
            classpath deps.plugin.android
            classpath deps.plugin.kotlin
            classpath deps.plugin.bintray
            classpath deps.plugin.maven
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
